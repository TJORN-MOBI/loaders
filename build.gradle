apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def localProps = new Properties()
localProps.load(project.rootProject.file('local.properties').newDataInputStream())

def androidPlatformVersion = 23

group 'mobi.tjorn.content'
version '1.0.0'
description 'Almost a drop-in replacement for AsyncTask.'

task makeReleaseJar(type: Jar) {
    dependsOn 'compileReleaseSources'
    baseName 'loaders'
    extension 'jar'
    from "$buildDir/intermediates/classes/release"
    exclude 'mobi/tjorn/content/loaders/R.class', 'mobi/tjorn/content/loaders/R$*.class'
}

task makeSourcesJar(type: Jar) {
    baseName 'loaders'
    classifier 'sources'
    extension 'jar'
    from 'src/main/java'
}

task makeJavadocs(type: Javadoc) {
    source 'src/main/java'
    destinationDir = file("$buildDir/docs/loaders")
    options {
        classpath new File(localProps.get('sdk.dir'), "platforms/android-$androidPlatformVersion/android.jar")
    }
}

task makeJavadocsJar(type: Jar) {
    dependsOn makeJavadocs
    baseName 'loaders'
    classifier 'javadoc'
    extension 'jar'
    from "$buildDir/docs/loaders"
}

publishing {
    publications {
        loaders(MavenPublication) {
            artifact makeReleaseJar
            artifact makeSourcesJar
            artifact makeJavadocsJar
            pom.withXml {
                asNode().appendNode('description', project.description)
            }
        }
    }
}

bintray {
    user = localProps.getProperty('bintray.user')
    key = localProps.getProperty('bintray.key')
    publications = ['loaders']
    pkg {
        repo = 'maven'
        name = 'loaders'
        userOrg = 'tjorn-mobi'
        desc = project.description
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/TJORN-MOBI/loaders.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'Initial version'
            vcsTag = '1.0.0'
            gpg {
                sign = true
                passphrase = localProps.getProperty('bintray.gpg.passphrase')
            }
        }
    }
}

bintrayUpload.dependsOn makeReleaseJar, makeSourcesJar, makeJavadocsJar, 'generatePomFileForLoadersPublication'

android {
    compileSdkVersion androidPlatformVersion
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

